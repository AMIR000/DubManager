<?xml version="1.0" encoding="utf-8"?>
<s:GridItemRenderer xmlns:fx="http://ns.adobe.com/mxml/2009"
                    xmlns:s="library://ns.adobe.com/flex/spark">
    <s:states>
        <s:State name="hovered"/>
        <s:State name="down"/>
        <s:State name="selected"/>
        <s:State name="normal"/>
        <s:State name="hoveredOverdue" stateGroups="overdue"/>
        <s:State name="downOverdue" stateGroups="overdue"/>
        <s:State name="selectedOverdue" stateGroups="overdue"/>
        <s:State name="normalOverdue" stateGroups="overdue"/>
        <s:State name="hoveredWarn" stateGroups="warn"/>
        <s:State name="downWarn" stateGroups="warn"/>
        <s:State name="selectedWarn" stateGroups="warn"/>
        <s:State name="normalWarn" stateGroups="warn"/>
    </s:states>
    <fx:Script>
        <![CDATA[
        import ru.kokorin.dubmanager.domain.Anime;
        import ru.kokorin.dubmanager.domain.AnimeStatus;
        import ru.kokorin.dubmanager.domain.Episode;
        import ru.kokorin.dubmanager.domain.EpisodeStatus;
        import ru.kokorin.dubmanager.domain.EpisodeType;

        private var stateExtension:String = "";

        override public function set data(value:Object):void {
            super.data = value;
            stateExtension = getStateExtension(value as Anime);
            setCurrentState(getCurrentRendererState(), false);
        }


        //TODO mark scheduled anime with green IF date of first episode is known
        override protected function getCurrentRendererState():String {
            var result:String = super.getCurrentRendererState();

            const extendedResult:String = result + stateExtension;
            if (hasState(extendedResult)) {
                result = extendedResult;
            }

            return result;
        }

        private static function getStateExtension(anime:Anime):String {
            var result:String = "";

            if (anime && anime.status == AnimeStatus.IN_PROCESS) {
                var nextEpisodeTime:Number = Number.MAX_VALUE;
                for each(var episode:Episode in anime.episodes) {
                    //Looking for latest completed episode
                    if (episode.status == EpisodeStatus.NOT_STARTED && episode.type == EpisodeType.REGULAR &&
                            episode.airDate && episode.airDate.time < nextEpisodeTime) {
                        nextEpisodeTime = episode.airDate.time;
                    }
                }

                const today:Date = new Date();
                today.hours = 23;
                today.minutes = 59;
                today.seconds = 59;
                const weekAgo:Date = new Date(today);
                weekAgo.date -= 7;

                if (nextEpisodeTime < weekAgo.time) {
                    result = "Overdue";
                } else if (nextEpisodeTime < today.time) {
                    result = "Warn";
                }
            }

            return result;
        }
        ]]>
    </fx:Script>
    <s:Rect top="0" bottom="0" right="0" left="0" includeIn="overdue,warn">
        <s:fill>
            <s:SolidColor color.overdue="red" color.warn="yellow" alpha="0.2"/>
        </s:fill>
    </s:Rect>
    <s:Label id="labelDisplay" top="10" bottom="10" left="10" right="10"
             verticalAlign="middle" lineBreak="explicit"/>
</s:GridItemRenderer>
